///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022-2025, ООО 1С-Рарус
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
//////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Производит инициализацию элементов справочника
//
Процедура ВыполнитьНачальноеЗаполнение() Экспорт
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ГруппыДанных.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ГруппыДанных КАК ГруппыДанных
	|ГДЕ
	|	ГруппыДанных.ИмяПредопределенныхДанных = ""ОБЩИЕ""";
	
	// Проверим существование предопределенного элемента
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() > 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 5
	|	ГруппыДанных.Ссылка КАК Ссылка,
	|	ГруппыДанных.Наименование КАК Наименование,
	|	ЭкземплярыДоступа.Количество КАК Количество
	|ИЗ
	|	Справочник.ГруппыДанных КАК ГруппыДанных
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ЭкземплярДоступа.ГруппаДанных КАК ГруппаДанных,
	|			СУММА(1) КАК Количество
	|		ИЗ
	|			Документ.ЭкземплярДоступа КАК ЭкземплярДоступа
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ЭкземплярДоступа.ГруппаДанных) КАК ЭкземплярыДоступа
	|		ПО ГруппыДанных.Ссылка = ЭкземплярыДоступа.ГруппаДанных
	|ГДЕ
	|	НЕ ГруппыДанных.ПометкаУдаления
	|	И (ГруппыДанных.Наименование ПОДОБНО ""основн%""
	|			ИЛИ ГруппыДанных.Наименование ПОДОБНО ""общ%"")
	|
	|УПОРЯДОЧИТЬ ПО
	|	Количество УБЫВ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество()=0 Тогда
		Объект = Справочники.ГруппыДанных.СоздатьЭлемент();
		Объект.Наименование = НСтр("ru = 'Общие'");
		Объект.Комментарий  = НСтр("ru = 'Создан при начальном заполнении'");
		Объект.УстановитьНовыйКод();
	Иначе
		Выборка.Следующий();
		Объект = Выборка.Ссылка.ПолучитьОбъект();
		Объект.ПометкаУдаления = ЛОЖЬ;
	КонецЕсли;
	Объект.ИмяПредопределенныхДанных = "Общие";
	Объект.Записать();
	
КонецПроцедуры // ВыполнитьНачальноеЗаполнение()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получить уникальные реквизиты.
// 
// Параметры:
//  Объект - СправочникОбъект.ВидыДанных
// 
// Возвращаемое значение:
//  Структура -  Получить уникальные реквизиты:
// * Наименование - Произвольный
//@skip-check constructor-function-return-section
Функция ПолучитьУникальныеРеквизиты(Объект) Экспорт
	
	// Структура уникальных реквизитов
	УникальныеРеквизиты = Новый Структура;
	УникальныеРеквизиты.Вставить("Наименование");
	
	// Возвращаем сформированные параметры проверки
	Возврат УникальныеРеквизиты;
	
КонецФункции // ПолучитьУникальныеРеквизиты()

#КонецОбласти

#КонецЕсли
