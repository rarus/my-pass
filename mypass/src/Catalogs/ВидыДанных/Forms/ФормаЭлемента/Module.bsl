///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2022-2025, ООО 1С-Рарус
// Все права защищены. Эта программа и сопроводительные материалы предоставляются
// в соответствии с условиями лицензии Attribution-ShareAlike 4.0 International (CC BY-SA 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by-sa/4.0/legalcode
//////////////////////////////////////////////////////////////////////////////////////////////////////

#Область ОбработчикиСобытийФормы

//@skip-check statement-type-change
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Цвет = ТекущийОбъект.Цвет.Получить();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Цвет = Новый ХранилищеЗначения(Цвет);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

//@skip-check invocation-parameter-type-intersect
&НаКлиенте
Процедура КомментарийОткрытие(Элемент, СтандартнаяОбработка)
	
	// Отказываемся от стандартной обработки события.
	СтандартнаяОбработка = Ложь;
	
	// Формируем описание обработчика перехвата закрытия формы.
	ОбработкаРезультатаЗакрытия = Новый ОписаниеОповещения("Подключаемый_ОбработкаРезультатаОповещения", ЭтотОбъект, "РасширенноеРедактированиеПоляКомментарий");
	
	// Открываем диалог многострочного редактирования текста комментария.
	ПоказатьВводСтроки(ОбработкаРезультатаЗакрытия, СокрЛП(Объект.Комментарий), НСтр("ru = 'Введите комментарий'") + " ...", , Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваВидаДанныхПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование Тогда
		Элемент.ТекущиеДанные.Отображать = ИСТИНА;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбработчикиСлужебногоПрограммногоИнтерфейса

// Подключаемый обработка результата оповещения.
// 
// Параметры:
//  РезультатОповещения - Строка - Результат оповещения
//  ДополнительныеПараметры - Неопределено -  Дополнительные параметры
&НаКлиенте
Процедура Подключаемый_ОбработкаРезультатаОповещения(РезультатОповещения, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если ДополнительныеПараметры = "РасширенноеРедактированиеПоляКомментарий" Тогда
		
		Если НЕ Объект.Комментарий = СокрЛП(РезультатОповещения) Тогда
			Объект.Комментарий = СокрЛП(РезультатОповещения);
			Модифицированность = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвойстваВидаДанныхВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СвойстваВидаДанных.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Поле.Имя = "СвойстваВидаДанныхОтображать" Тогда
		
		ТекущиеДанные.Отображать = НЕ ТекущиеДанные.Отображать;
		Модифицированность = Истина;
		
	ИначеЕсли Поле.Имя = "СвойстваВидаДанныхСвойство" Тогда
		Возврат;
		
	Иначе
		
		ПараметрыОткрытия = Новый Структура();
		ПараметрыОткрытия.Вставить("Ключ", ТекущиеДанные.Свойство);
		ОткрытьФорму("ПланВидовХарактеристик.СвойстваВидовДанных.Форма.ФормаЭлемента", ПараметрыОткрытия, ЭтотОбъект);
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
